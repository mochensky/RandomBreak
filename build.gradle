plugins {
    id 'fabric-loom' version '1.10-SNAPSHOT'
    id 'maven-publish'
}

version = project.mod_version
group = project.maven_group
base {
    archivesName = project.archives_base_name
}

def supportedVersionRanges = [
        '1.20-1.20.4': [
                minecraft_version: '1.20.4',
                yarn_mappings: '1.20.4+build.3',
                fabric_version: '0.97.3+1.20.4',
                loader_version: '0.16.14',
                minecraft_version_range: '>=1.20 <1.20.5',
                java_version: 17
        ],
        '1.20.5-1.20.6': [
                minecraft_version: '1.20.6',
                yarn_mappings: '1.20.6+build.3',
                fabric_version: '0.100.8+1.20.6',
                loader_version: '0.16.14',
                minecraft_version_range: '>=1.20.5 <1.21',
                java_version: 17
        ],
        '1.21.x': [
                minecraft_version: '1.21.6',
                yarn_mappings: '1.21.6+build.1',
                fabric_version: '0.127.1+1.21.6',
                loader_version: '0.16.14',
                minecraft_version_range: '>=1.21 <1.22',
                java_version: 21
        ]
]

def targetVersionRange = project.hasProperty('minecraft_version_range') ? project.minecraft_version_range : '1.21.x'
def versionConfig = supportedVersionRanges[targetVersionRange]

if (!versionConfig) {
    throw new GradleException("Unsupported Minecraft version range: ${targetVersionRange}. Supported ranges: ${supportedVersionRanges.keySet()}")
}

dependencies {
    minecraft "com.mojang:minecraft:${versionConfig.minecraft_version}"
    mappings "net.fabricmc:yarn:${versionConfig.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${versionConfig.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${versionConfig.fabric_version}"
}

processResources {
    inputs.property "version", project.version
    inputs.property "minecraft_version_range", versionConfig.minecraft_version_range
    inputs.property "loader_version", versionConfig.loader_version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version,
                "minecraft_version_range": versionConfig.minecraft_version_range,
                "loader_version": versionConfig.loader_version
    }
}

def targetJavaVersion = versionConfig.java_version

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release.set(targetJavaVersion as Integer)
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion as String)
    }
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

remapJar {
    archiveFileName = "${project.archives_base_name}-${project.mod_version}-mc${targetVersionRange}.jar"
}

publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = project.archives_base_name + "-${targetVersionRange}"
            from components.java
        }
    }
    repositories {
    }
}
